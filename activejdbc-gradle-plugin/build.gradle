group 'org.javalite'
version '1.4.12-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'org.javalite', name: 'activejdbc', version: '1.4.11'
    compile group: 'org.javalite', name: 'activejdbc-instrumentation', version: '1.4.11'

    deployerJars group: 'org.apache.maven.wagon', name: 'wagon-http', version: '2.2'
}

task docJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives docJar
    archives sourceJar
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                // authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            pom.project {
                packaging 'jar'
                description 'Gradle plugin for performing build time ActiveJDBC instrumentation'
                url 'https://github.com/javalite/activejdbc/'
                scm {
                    url 'scm:git:git@github.com:javalite/activejdbc.git'
                    connection 'scm:git:git@github.com:javalite/activejdbc.git'
                    developerConnection 'git@github.com:javalite/activejdbc.git'
               }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

